//macros used for organic ships
LIVING_HULL_EFFECTS_GROUPS
'''EffectsGroup
    scope = Source
    activation = Source
    effects = [
        SetStructure value = Value + 2
        SetDetection value = Value + 50
    ]

[[LIVING_HULL_BASE_FUEL_REGEN]]
'''

LIVING_HULL_BASE_FUEL_REGEN
'''EffectsGroup
    description = "LIVING_HULL_BASE_FUEL_REGEN_DESC"
    scope = Source
    activation = And [
        Turn low = Source.ArrivedOnTurn + 1
        Stationary
        (Source.Fuel < Source.MaxFuel)
    ]
    stackinggroup = "BASE_FUEL_REGEN"
    accountinglabel = "BASE_FUEL_REGEN_LABEL"
    priority = [[LATE_PRIORITY]]
    effects = SetFuel value = min(Target.MaxFuel, Value + 0.3001)

   [[REFUEL_MESSAGE(0.3001)]]
'''

// @1@ Structure increase per turn
// @2@ Max structure increase
ORGANIC_GROWTH
'''EffectsGroup
    scope = Source
    activation = Source
    accountinglabel = "ORGANIC_GROWTH"
    effects = SetMaxStructure value = Value + min(Source.Age * @1@, @2@)
'''

#include "/scripting/common/priorities.macros"
